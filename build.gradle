plugins {
    id 'java'
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version '8.1.1' apply false
}

allprojects {
    apply plugin: "java"

    group = 'xyz.tcbuildmc.minecraft'
    version = '1.0.0-SNAPSHOT'
    base.archivesName = "HorizontalNekoPlugin-" + project.name

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "JitPack"
            url = "https://jitpack.io/"
        }
        maven {
            name = "Papi"
            url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
        }
        maven {
            name = "CodeMC"
            url = "https://repo.codemc.org/repository/maven-public/"
        }
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    def targetJavaVersion = 17
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }

        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    compileJava.dependsOn(clean)

    jar {
        zip64 true
    }

    sourcesJar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compileOnly "net.luckperms:api:5.4"

        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        compileOnly 'org.projectlombok:lombok:1.18.32'

        implementation "com.electronwill.night-config:toml:3.6.7"
    }

    shadowJar {
        dependencies {
            include dependency("com.electronwill.night-config:core:3.6.7")
            include dependency("com.electronwill.night-config:toml:3.6.7")
        }

        def reloc = { String from, String dest ->
            relocate from, dest
        }

        reloc "com.electronwill.nightconfig", "xyz.tcbuildmc.minecraft.hnp.nightconfig"

        zip64 true
    }

    publishing {
        publications {
            MavenJava(MavenPublication) {
                artifactId = "HorizontalNekoPlugin-" + project.name
                from components.java
            }
        }

        repositories {
            mavenLocal()
        }
    }
}

tasks.register("buildArtifacts") {
    logger.lifecycle(":step1 build subprojects")

    subprojects {
        dependsOn project.tasks.named("build").get()
        dependsOn project.tasks.named("shadowJar").get()
    }

    doFirst {
        logger.lifecycle(":step2 clean buildLibs of rootProject")
        delete fileTree(rootProject.buildDir.toPath().resolve("libs")) {
            include '*'
        }

        logger.lifecycle(":step3 copy buildLibs from subprojects")
        subprojects {
            copy {
                from(project.buildDir.toPath().resolve("libs")) {
                    include "*.jar"
                }

                into(rootProject.buildDir.toPath().resolve("libs"))
                duplicatesStrategy DuplicatesStrategy.EXCLUDE
            }
        }
    }

    doLast {
        logger.lifecycle(":buildArtifacts done")
    }
}
